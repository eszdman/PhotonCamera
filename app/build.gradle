apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    def versionPropsFile = file('version.properties')
    def versionBuild
    compileSdkVersion 32
    buildToolsVersion '30.0.3'
    lintOptions {
        abortOnError false
    }

    buildFeatures {
        dataBinding true
        mlModelBinding true
    }

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
    } else {
        throw new FileNotFoundException("Could not read version.properties!")
    }

    ext.autoIncrementBuildNumber = {
        if (versionPropsFile.canRead()) {
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new FileNotFoundException("Could not read version.properties!")
        }
    }

    defaultConfig {
        applicationId 'com.particlesdevs.photoncamera'
        minSdkVersion 26
        targetSdkVersion 32
        versionCode versionBuild
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "arm64-v8a"
        }
        versionName '0.8'
    }

    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(assembleDebug)) {
            autoIncrementBuildNumber()
        } else if (taskGraph.hasTask(assembleRelease)) {
            autoIncrementBuildNumber()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            jniDebuggable = false
            zipAlignEnabled = true
            signingConfig signingConfigs.debug
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            jniDebuggable = true
            zipAlignEnabled = true
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    applicationVariants.all {
        variant -> variant.outputs.all {
            outputFileName = "PhotonCamera-${versionName}${versionBuild}-${variant.name}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':circularbarlib')
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'org.jetbrains:annotations:16.0.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.github.mohammadatif:Animatoo:master'
    implementation 'com.github.ChickenHook:RestrictionBypass:2.2'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation project(path: ':photonbypass')
    implementation 'org.tensorflow:tensorflow-lite-support:0.1.0'
    implementation 'org.tensorflow:tensorflow-lite-metadata:0.1.0'
    implementation 'org.tensorflow:tensorflow-lite-gpu:2.3.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation group: 'commons-io', name: 'commons-io', version: '20030203.000550'
    implementation 'rapid.decoder:library:0.3.0'
    implementation 'rapid.decoder:jpeg-decoder:0.3.0'
    implementation 'rapid.decoder:png-decoder:0.3.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    // Navigation Component
    implementation "androidx.navigation:navigation-fragment:2.3.5"
    implementation "androidx.navigation:navigation-ui:2.3.5"
    //Subsampling image view
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    //Google Material
    implementation 'com.google.android.material:material:1.6.0-alpha01'
    implementation "androidx.core:core-ktx:+"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //Leak Canary
    //Use this lib to detect memory leaks when debugging
    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'
}
repositories {
    mavenCentral()
}